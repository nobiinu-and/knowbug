// knowbug_client proxy

#include "hspext.as"
#include "hspcmp.as"

#packopt name "knowbug_client_proxy"
#packopt hide 1

#define true 1
#define false 0

#define gsel_hide (-1)
#define gsel_show 1
#define mesbox_readonly 0
#define objmode_font 2

#define EXIT_FAILURE 1

#define s_default_window_id 0

	gsel s_default_window_id, gsel_hide

	s_obj_name = "knowbug_client.ax"
	s_runtime_name = ""
	s_compile_opts = 1 // 1: デバッグ情報を埋め込む
	s_pp_opts = 16 | 32 // utf-8
	s_debug_mode = 1 // 1: デバッグウィンドウ表示
	s_exit_code = EXIT_FAILURE

	sdim s_error, 0x10000
	sdim s_config
	sdim s_src_name
	sdim s_hsp_root

	sdim s_buffer
	sdim s_line

	onexit goto *l_on_exit

	gosub *l_load_config

	gosub *l_run
	if stat == false {
		goto *l_fail
	}

	s_exit_code = 0
	end

*l_load_config

	notesel s_config
	noteload dir_exe + "\\knowbug_client_proxy.txt"
	noteget s_src_name, 0
	noteget s_hsp_root, 1
	noteunsel
	return

*l_run

	hsc_ini s_src_name
	hsc_objname s_obj_name
	hsc_compath s_hsp_root + "\\common\\"

	hsc_comp s_compile_opts, s_pp_opts, s_debug_mode
	if stat {
		hsc_getmes s_error
		return false
	}

	hsc3_getruntime s_runtime_name, s_obj_name
	if s_runtime_name == "" {
		s_runtime_name = "hsp3.exe"
	}

	s_runtime_path = s_hsp_root + "\\" + s_runtime_name

	s_cmd = "\"" + s_runtime_path + "\" " + s_obj_name + " " + dirinfo(4) // FIXME: dir_cmdline is undefined?
	pipeexec s_buffer, s_cmd, true // true = show window
	if stat {
		s_error = "Could not execute: '" + s_cmd + "'"
		return false
	}

	repeat
		pipeget s_line
		if stat == 0 {
			break
		}
		await 100
	loop
	return true

*l_on_exit

	end s_exit_code

*l_fail

	title "knowbug_client_proxy"

	font "MS Gothic", 12
	objmode objmode_font
	mesbox s_error, ginfo_winx, ginfo_winy, mesbox_readonly

	gsel s_default_window_id, gsel_show
	stop
