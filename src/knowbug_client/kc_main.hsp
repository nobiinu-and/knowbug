// エントリーポイント

#include "kc_app.hsp"

#module m_infra

#define true 1
#define false 0

// C+ のヘッダファイルだが、HSP としても読めるようになっているので、include できる。
#include "knowbug_protocol.h"

#deffunc infra_connect_server

	infra_init_globals
	infra_init_subs

	infra_parse_cmdline
	if stat == false {
		return false
	}

	infra_connect_server_buffer
	if stat == false {
		return false
	}

	infra_connect_client_buffer
	if stat == false {
		return false
	}
	return true

#deffunc infra_init_globals

	sdim s_error

	// サーバー側のウィンドウハンドル
	dim s_server_hwnd

	// サーバー側のデータ交換バッファー
	dim s_server_buffer_handle
	dim s_server_buffer_view
	sdim s_server_buffer_data

	// クライアント側のデータ交換バッファー
	dim s_client_buffer_handle
	dim s_client_buffer_view
	sdim s_client_buffer_data
	return

#deffunc infra_onexit onexit

	sdim s_server_buffer_data
	if s_server_buffer_view {
		UnmapViewOfFile s_server_buffer_view
	}
	if s_server_buffer_handle {
		CloseHandle s_server_buffer_handle
	}

	sdim s_client_buffer_data
	if s_client_buffer_view {
		UnmapViewOfFile s_client_buffer_view
	}
	if s_client_buffer_handle {
		CloseHandle s_client_buffer_handle
	}
	return

// -----------------------------------------------
// コマンドライン
// -----------------------------------------------

#deffunc infra_parse_cmdline

	if dir_cmdline == "" {
		dialog "コマンドラインが空です。", dialog_warn
		return false
	}

	s_server_hwnd = int(dirinfo(4)) // dir_cmdline not defined?
	return true

// -----------------------------------------------
// メモリマップトファイル
// -----------------------------------------------

#deffunc infra_connect_memory_mapped_file str file_name, var file_handle, var view, var data

	OpenFileMapping FILE_MAP_READ | FILE_MAP_WRITE, false, file_name
	file_handle = stat
	if file_handle == NULL {
		debug_warn "デバッグウィンドウの初期化に失敗しました。(クライアントがデータ交換バッファーにアクセスできませんでした。)"
		return false
	}

	MapViewOfFile file_handle, FILE_MAP_READ | FILE_MAP_WRITE, 0, 0, 0
	view = stat
	if view == NULL {
		debug_warn "デバッグウィンドウの初期化に失敗しました。(クライアントがデータ交換バッファーのビューを作成できませんでした。)"
		return false
	}

	dupptr data, view, MEMORY_BUFFER_SIZE, vartype("str")
	return true

#deffunc infra_connect_server_buffer

	infra_connect_memory_mapped_file "KnowbugServerBuffer", s_server_buffer_handle, s_server_buffer_view, s_server_buffer_data
	return stat

#deffunc infra_connect_client_buffer

	infra_connect_memory_mapped_file "KnowbugClientBuffer", s_client_buffer_handle, s_client_buffer_view, s_client_buffer_data
	return stat

// -----------------------------------------------
// サーバーへのメッセージ
// -----------------------------------------------

#deffunc infra_send_hello

	sendmsg s_server_hwnd, KMTS_HELLO, 0, hwnd
	return

#deffunc infra_send_terminate

	sendmsg s_server_hwnd, KMTS_TERMINATE
	return

#deffunc infra_send_step_continue

	sendmsg s_server_hwnd, KMTS_STEP_CONTINUE
	return

#deffunc infra_send_step_pause

	sendmsg s_server_hwnd, KMTS_STEP_PAUSE
	return

#deffunc infra_send_step_in

	sendmsg s_server_hwnd, KMTS_STEP_IN
	return

#deffunc infra_send_location_update

	sendmsg s_server_hwnd, KMTS_LOCATION_UPDATE
	return

#deffunc infra_send_source int source_file_id

	sendmsg s_server_hwnd, KMTS_SOURCE, source_file_id
	return

#deffunc infra_send_list_update

	sendmsg s_server_hwnd, KMTS_LIST_UPDATE
	return

#deffunc infra_send_list_toggle_expand int item_index

	sendmsg s_server_hwnd, KMTS_LIST_TOGGLE_EXPAND, item_index
	return

#deffunc infra_send_list_details int item_index

	sendmsg s_server_hwnd, KMTS_LIST_DETAILS, item_index
	return

// -----------------------------------------------
// サーバーからのメッセージ
// -----------------------------------------------

#deffunc infra_init_subs

	oncmd gosub *l_on_hello_ok, KMTC_HELLO_OK
	oncmd gosub *l_on_shutdown, KMTC_SHUTDOWN
	oncmd gosub *l_on_logmes, KMTC_LOGMES
	oncmd gosub *l_on_stopped, KMTC_STOPPED
	oncmd gosub *l_on_location, KMTC_LOCATION
	oncmd gosub *l_on_source_path, KMTC_SOURCE_PATH
	oncmd gosub *l_on_source_code, KMTC_SOURCE_CODE
	oncmd gosub *l_on_list_update_ok, KMTC_LIST_UPDATE_OK
	oncmd gosub *l_on_list_details_ok, KMTC_LIST_DETAILS_OK
	return

*l_on_hello_ok

	app_did_receive_hello_ok
	return

*l_on_shutdown

	app_did_receive_shutdown
	return

*l_on_logmes

	app_did_receive_logmes s_server_buffer_data
	return

*l_on_stopped

	app_did_receive_stopped wparam, lparam
	return

*l_on_location

	app_did_receive_location wparam, lparam
	return

*l_on_source_path

	app_did_receive_source_path wparam, s_server_buffer_data
	return

*l_on_source_code

	app_did_receive_source_code wparam, s_server_buffer_data
	return

*l_on_list_update_ok

	app_did_receive_list_update_ok s_server_buffer_data
	return

*l_on_list_details_ok

	app_did_receive_list_details_ok s_server_buffer_data
	return

#global

	app_init
