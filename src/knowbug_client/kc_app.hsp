// アプリケーションのコア部分

#packopt name "knowbug_client"
#packopt hide 1

#include "hsp3utf.as"
#include "kernel32.as"
#include "user32.as"

// -----------------------------------------------
// HSP の定数
// -----------------------------------------------

#define global dialog_warn 1

#define global gsel_hide (-1)
#define global gsel_show 1

#define global mesbox_readonly 0

#define global objmode_font 2

// -----------------------------------------------
// Win32 API の定数
// -----------------------------------------------

#define global NULL            0
#define global EXIT_FAILURE    1
#define global FILE_MAP_WRITE  0x0002
#define global FILE_MAP_READ   0x0004
#define global WM_USER         0x0400

// -----------------------------------------------
// ヘルパー
// -----------------------------------------------

//#ifdef _DEBUG
#define global debug_trace(%1) s_log_text@m_app += "TRACE: " + (%1) + "\n" : app_log_did_update
#define global debug_warn(%1) s_log_text@m_app += "WARN: " + (%1) + "\n" : app_log_did_update
//#else
//#define global debug_trace(%1) :
//#endif

// -----------------------------------------------
// アプリケーション
// -----------------------------------------------

#module m_app

#define true 1
#define false 0

// FIXME: エンコーディングとプラットフォームも表示
#define s_title "knowbug v0.1.0"

#enum s_default_window_id = 0

#deffunc app_init

	app_init_globals
	app_init_windows

	infra_connect_server
	if stat == false {
		app_did_disconnect
		return
	}

	logmes "send hello"
	infra_send_hello
	return

#deffunc app_init_globals

	// サーバーと接続しているか？
	s_connected = false

	// デバッギが実行中か？
	s_debuggee_is_running = true

	// logmes の出力結果
	sdim s_log_text, 0x10000
	return

#deffunc app_init_windows \
	local x

	// gsel s_default_window_id, gsel_hide
	width , , ginfo_dispx - ginfo_winx - 16, ginfo_dispy - ginfo_winy - 64

	title s_title + " [接続待ち]"

	s_step_button_width = 80
	s_step_control_height = 24
	s_step_control_top = ginfo_winy - s_step_control_height - 8

	font "MS Gothic", 12
	objmode objmode_font
	mesbox s_log_text, ginfo_winx, ginfo_winy - 8 - s_step_control_height - 8, mesbox_readonly
	s_log_edit_id = stat

	objsize s_step_button_width, s_step_control_height
	x = 8

	pos x, s_step_control_top
	button gosub "実行", *l_step_continue_button
	x += s_step_button_width + 8

	pos x, s_step_control_top
	button gosub "停止", *l_step_pause_button
	x += s_step_button_width + 8

	pos x, s_step_control_top
	button gosub "次へ", *l_step_in_button
	x += s_step_button_width + 8

	gsel s_default_window_id, gsel_show
	return

// -----------------------------------------------
// ステップ実行ボタン
// -----------------------------------------------

#deffunc app_step_continue

	logmes "app_step_continue"
	infra_send_step_continue
	return

*l_step_continue_button

	app_step_continue
	return

#deffunc app_step_pause

	logmes "app_step_pause"
	infra_send_step_pause
	return

*l_step_pause_button

	app_step_pause
	return

#deffunc app_step_in

	logmes "app_step_in"
	infra_send_step_in
	return

*l_step_in_button

	app_step_in
	return

// -----------------------------------------------
// サーバーからのメッセージへの応答
// -----------------------------------------------

#deffunc app_did_receive_hello_ok

	logmes "app_did_receive_hello_ok"
	s_connected = true
	title s_title
	return

#deffunc app_did_receive_shutdown

	end

#deffunc app_did_receive_logmes var text

	s_log_text += text + "\n"
	app_log_did_update
	return

#deffunc app_did_receive_stopped

	logmes "stopped"
	s_debuggee_is_running = false
	return

// -----------------------------------------------
// その他
// -----------------------------------------------

#deffunc app_did_disconnect

	title s_title + " [接続なし]"
	return

#deffunc app_log_did_update

	objprm s_log_edit_id, s_log_text
	return

#global
