// test - knowbug

// インスタンス(Module-Instance)の拡張表示を確認します。

#module MCTree mName, mId, mChildren

#modinit str name
	dimtype mChildren, vartype("struct")
	mName = name
	mId   = stt_uniqueId
	
	stt_uniqueId ++		// モジュール内の静的変数
	return
	
#modterm
	foreach mChildren
		delmod mChildren(cnt)
	loop
	return
	
#modfunc tree_addChild str name
	newmod mChildren, MCTree@, name
	return
	
#modfunc tree_getChild var vRes, int n
	vRes = mChildren(n)
	return
	
#global

*main
	logmes "BEGIN"
	
	newmod tree, MCTree, "root"
	root = tree(0)
	
	repeat 2
		
		tree_addChild root, "node#" + cnt
		tree_getChild root, node, cnt
		
		repeat 2
			tree_addChild node, "leaf#" + cnt
			tree_getChild node, leaf, cnt
		loop
		
	loop
	
	logmes "END"
	stop
	
